扩充C0文法 （此文档为文法示例，不能用于作业）  
 
＜加法运算符＞	::= +｜-
＜乘法运算符＞ 	::= *｜/
＜关系运算符＞ 	::=  <｜<=｜>｜>=｜!=｜==
＜字母＞ 		::= ＿｜a｜．．．｜z｜A｜．．．｜Z
＜数字＞ 		::= ０｜＜非零数字＞
＜非零数字＞ 	::= １｜．．．｜９
＜专用符号＞ 	::= （｜）｜｛｜｝｜,｜;｜＝
＜字符＞  		::=	 ‘<加法运算符＞｜＜乘法运算符＞｜＜字母＞｜＜数字＞’
＜字符串＞ 		::=	 "｛十进制编码为32,33,35-126的ASCII字符｝" 
＜程序＞ 			::= ［＜常量说明部分＞］［＜变量说明部分＞］{＜有返回值函数定义部分＞}{＜无返回值函数定义部分＞}＜主函数＞
＜常量说明部分＞	::= 	const＜常量定义＞;{ const＜常量定义＞;}
＜常量定义＞ 		::= 	int＜标识符＞＝＜整数＞{,＜标识符＞＝＜整数＞}
							|float＜标识符＞＝＜实数＞{,＜标识符＞＝＜实数＞}
							| char＜标识符＞＝＜字符＞{,＜标识符＞＝＜字符＞}
＜整数＞ 			::= ［＋｜－］＜非零数字＞｛＜数字＞｝｜０
＜实数＞ 			::= ［＋｜－］<整数>[.<整数>]
＜标识符＞ 			::=  ＜字母＞｛＜字母＞｜＜数字＞｝
＜声明头部＞ 		::=	＜类型标识符＞＜标识符＞
＜变量说明部分＞ 	::=	<变量定义>;{<变量定义>;}
＜变量定义＞		::= ＜类型标识符＞＜标识符＞{,＜标识符＞}
＜类型标识符＞      ::=  int | float | char
＜有返回值函数定义部分＞ 	::= 	＜声明头部＞‘(’ ＜参数表＞ ‘)’  ‘{’ ＜复合语句＞ ‘}’
＜无返回值函数定义部分＞ 	::=	void<标识符>‘(’ <参数表> ‘)’  ‘{’  <复合语句> ‘}’
＜复合语句＞ 		::= 	［＜常量说明部分＞］［＜变量说明部分＞］＜语句列＞
＜参数表＞ 			::=  ＜类型标识符＞＜标识符＞{,＜类型标识符＞＜标识符＞}| 空
＜主函数＞ 			::=	void main ‘(’ ＜参数＞ ‘)’  ‘{’ ＜复合语句＞ ‘}’
＜表达式＞ 			::= ［＋｜－］＜项＞{＜加法运算符＞＜项＞}
＜项＞ 				::= ＜因子＞{＜乘法运算符＞＜因子＞}
＜因子＞ 			::= ＜标识符＞｜‘(’＜表达式＞‘)’｜＜整数＞｜＜有返回值函数调用语句＞|<实数>|＜字符＞       			//返回值为char型的函数和＜字符＞, 用字符的ASCII码参加计算
＜语句＞ 			::= ＜条件语句＞｜＜循环语句＞｜‘{’<语句列>‘}’｜＜返回语句＞;|＜有返回值函数调用语句＞; |＜无返回值函数调用语句＞；｜＜赋值语句＞;｜＜读语句＞;｜＜写语句＞;｜＜空＞
＜赋值语句＞ 		::= 	＜标识符＞＝＜表达式＞
＜条件语句＞		::= 	if ‘(’＜条件＞‘)’＜语句＞［else＜语句＞］
＜条件＞ 			::=  ＜表达式＞＜关系运算符＞＜表达式＞｜＜表达式＞ //表达式为0条件为假，否则为真
＜循环语句＞ 		::= 	while ‘(’＜条件＞‘)’＜语句＞| for‘(’ ＜标识符＞＝＜表达式＞；<条件>；＜标识符＞＝＜表达式＞‘)’<语句>  
＜有返回值函数调用语句＞ ::= ＜标识符＞‘(’＜值参数表＞‘)’
＜无返回值函数调用语句＞ ::= ＜标识符＞‘(’＜值参数表＞‘)’
＜值参数表＞ 		::= ＜表达式＞{,＜表达式＞}｜＜空＞
＜语句列＞			::= ＜语句＞｛＜语句＞｝
＜读语句＞ 			::= 	scanf ‘(’＜标识符＞‘)’
＜写语句＞ 			::=	printf ‘(’[<字符串>,][＜表达式 ＞] ‘)’
＜返回语句＞ 		::= 	return[‘(’＜表达式＞‘)’]
           